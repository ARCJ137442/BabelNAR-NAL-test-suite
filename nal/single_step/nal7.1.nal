' File: nal7.1.nal
' Author: OpenNARS Team
' Editor: @ARCJ137442
' temporal induction/comparison 时间归纳对比
' * 📝【2024-05-14 21:31:55】在OpenNARS 3.1.2中成功
' ! 📝【2024-05-14 21:30:09】NAL-8、NAL-9 均在OpenNARS 3.1.2上表现不佳——未能完全实现功能

' * 🕒延迟0.2秒，给启动留足时间
''sleep: 0.2s

' * 🔊允许所有导出结论被展示（用于OpenNARS）
'''VOL 100

' * 💭有人在进入 101号房间 前打开了 101号门
$0.80;0.80;0.95$ <<(*,$x,door_101) --> open> =/> <(*,$x,room_101) --> enter>>. %0.90;0.90%

' * 💭有人在拿到 101号钥匙 后打开了 101号门
$0.80;0.80;0.95$ <<(*,$y,door_101) --> open> =\> <(*,$y,key_101) --> hold>>. %0.80;0.90%

' * 💡若某人拿到了 101号钥匙，TA将进入 101号房间
''expect-cycle(10,1,0.1s): OUT <<(*,$1,key_101) --> hold> =/> <(*,$1,room_101) --> enter>>. %0.90;0.39%

' * 💡若某人进入了 101号房间，那TA应该拿到过 101号钥匙
''expect-cycle(10,1,0.1s): OUT <<(*,$1,room_101) --> enter> =\> <(*,$1,key_101) --> hold>>. %0.80;0.42%

' * 💡若某人拿到了 101号钥匙，这意味着TA将进入 101号房间
''expect-cycle(10,1,0.1s): OUT <<(*,$1,key_101) --> hold> </> <(*,$1,room_101) --> enter>>. %0.73;0.44%

' * 🔚退出测试
''terminate(if-no-user)
