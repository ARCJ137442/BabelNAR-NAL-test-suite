' file: 6.2.nal
' Author: OpenNARS Team
' Editor: @ARCJ137442
' variable unification 统一+与或归纳等价
' 📝使用`'//`对指令进行注释

' * 🕒延迟0.2秒，给启动留足时间
''sleep: 0.2s

' * 🔊允许所有导出结论被展示（用于OpenNARS）
'''VOL 100

' * 💭若 某物是一种天鹅 则 它是一种鸟
$0.80;0.80;0.95$ <<$x --> swan> ==> <$x --> bird>>. %1.00;0.80%

' * 💭若 某物是一种天鹅 则 它会游泳
$0.80;0.80;0.95$ <<$y --> swan> ==> <$y --> swimmer>>. %0.80;0.90%

' * 💡若 某物是一种天鹅 则 它是一种鸟 或 它会游泳
''expect-cycle(10,1,0.05s): OUT <<$1 --> swan> ==> (||,<$1 --> bird>,<$1 --> swimmer>)>. %1.00;0.72%

' * 💡一般来说 若 某物是一种天鹅 则 它是一种会游泳的鸟
''expect-cycle(10,1,0.05s): OUT <<$1 --> swan> ==> (&&,<$1 --> bird>,<$1 --> swimmer>)>. %0.80;0.72%

' * 💡或许 若 某物会游泳 则 它是一种鸟
''expect-cycle(10,1,0.05s): OUT <<$1 --> swimmer> ==> <$1 --> bird>>. %1.00;0.37%

' * 💡或许 若 某物是一种鸟 则 它会游泳
''expect-cycle(10,1,0.05s): OUT <<$1 --> bird> ==> <$1 --> swimmer>>. %0.80;0.42%

' * 💡或许 某物是一种鸟 当且仅当 它会游泳
''expect-cycle(10,1,0.05s): OUT <<$1 --> bird> <=> <$1 --> swimmer>>. %0.80;0.42%

' * 🔚退出测试
''terminate(if-no-user)
